app applyai {
  wasp: {
    version: "^0.9.0"
  },
  title: "CoverLetterGPT",
  head: [
        "<meta property='og:type' content='website' />",
        "<meta property='og:url' content='https://CoverLetterGPT.xyz' />",
        "<meta property='og:description' content='Create Unique Cover Letters Based on Your Skills. Supercharge Your Job Search.' />",
        "<meta property='og:image' content='src/client/static/coverlettegpt.png' />"
  ],
  // üîê Auth out of the box! https://wasp-lang.dev/docs/language/features#authentication--authorization
  auth: {
    userEntity: User,
    externalAuthEntity: SocialLogin,
    methods: {
      google: {
        getUserFieldsFn: import { getUserFields } from "@server/auth.js",
        configFn: import { config } from "@server/auth.js",
      },
    },
    onAuthFailedRedirectTo: "/",
  },
  db: {
    system: PostgreSQL
  },
  server: {
    setupFn: import serverSetup from "@server/serverSetup.js"
  },
  client: {
    rootComponent: import App from "@client/App",
    setupFn: import clientSetup from "@client/clientSetup.js"
  },

  dependencies: [
    ("node-fetch", "3.3.0"),
    ("@chakra-ui/react", "1.8.8"),
    ("react-icons", "4.8.0"),
    ("@emotion/react", "11.10.6"),
    ("@emotion/styled", "11.10.6"),
    ("framer-motion", "6.5.1"),
    ("react-hook-form", "7.43.1"),
    ("pdfjs-dist", "3.3.122"),
    ("@types/pdfjs-dist", "2.10.378"),
    ("stripe", "11.15.0"),
    ("micro", "10.0.1"),
    ("@sendgrid/mail", "7.7.0")
  ],
}

/* üíΩ Wasp defines DB entities via Prisma Database Models: 
 * https://wasp-lang.dev/docs/language/features#entity
 */

entity User {=psl
  id          Int     @id @default(autoincrement())
  username    String  @unique
  password    String
  email       String?
  hasPaid     Boolean @default(false)
  datePaid    DateTime?
  stripeId    String? 
  checkoutSessionId String?
  notifyPaymentExpires Boolean @default(false)
  credits     Int     @default(3)
  letters     CoverLetter[]
  jobs        Job[]
  externalAuthAssociations  SocialLogin[]
psl=}

entity SocialLogin {=psl
  id          String    @id @default(uuid())
  provider    String
  providerId  String
  user        User      @relation(fields: [userId], references: [id], onDelete: Cascade)
  userId      Int
  createdAt   DateTime  @default(now())
  @@unique([provider, providerId, userId])
psl=}

entity CoverLetter {=psl
  id         String     @id @default(uuid())
  title      String
  content    String
  tokenUsage Int
  job        Job        @relation(fields: [jobId], references: [id], onDelete: Cascade)
  jobId      String
  user       User?      @relation(fields: [userId], references: [id])
  userId     Int?
  createdAt  DateTime   @default(now())
  updatedAt  DateTime?  @updatedAt
psl=}

entity Job {=psl
  id            String         @id @default(uuid())
  title         String
  company       String
  location      String
  description   String
  coverLetter   CoverLetter[] 
  user          User?          @relation(fields: [userId], references: [id])
  userId        Int?
  createdAt     DateTime       @default(now())
  updatedAt     DateTime       @updatedAt
  isCompleted   Boolean        @default(false)
psl=}

/* üì° These are the Wasp Routes (You can protect them easily w/ 'authRequired: true');
 * https://wasp-lang.dev/docs/language/features#route
 */

route RootRoute { path: "/", to: MainPage }
page MainPage {
  component: import Main from "@client/MainPage"
}

route CoverLetterPage { path: "/cover-letter/:id", to: CoverLetterPage }
page CoverLetterPage {
  component: import { CoverLetter } from "@client/CoverLetter"
}

route LoginRoute { path: "/login", to: LoginPage }
page LoginPage {
  component: import Login from "@client/LoginPage"
}

route JobsRoute { path: "/jobs", to: JobsPage }
page JobsPage {
  authRequired: true,
  component: import Jobs from "@client/JobsPage"
}

route ProfileRoute { path: "/profile", to: ProfilePage }
page ProfilePage {
  authRequired: true,
  component: import Profile from "@client/ProfilePage"
}

route CheckoutRoute { path: "/checkout", to: CheckoutPage }
page CheckoutPage {
  authRequired: true,
  component: import Checkout from "@client/CheckoutPage"
}

/* ‚õë These are the Wasp Operations, which allow the client and server to interact:
 * https://wasp-lang.dev/docs/language/features#queries-and-actions-aka-operations
 */

// üìù Actions aka Mutations

action generateCoverLetter {
  fn: import { generateCoverLetter } from "@server/actions.js",
  entities: [CoverLetter, User]
}

action createJob {
  fn: import { createJob } from "@server/actions.js",
  entities: [Job]
}

action updateJob {
  fn: import { updateJob } from "@server/actions.js",
  entities: [Job]
}

action updateCoverLetter {
  fn: import { updateCoverLetter } from "@server/actions.js",
  entities: [Job, CoverLetter, User]
}

action generateEdit {
  fn: import { generateEdit } from "@server/actions.js",
  entities: [CoverLetter, User]
}

action editCoverLetter {
  fn: import { editCoverLetter } from "@server/actions.js",
  entities: [CoverLetter]
}

action updateUser {
  fn: import { updateUser } from "@server/actions.js",
  entities: [User]
}

action updateUserHasPaid {
  fn: import { updateUserHasPaid } from "@server/actions.js",
  entities: [User]
}

action deleteJob {
  fn: import { deleteJob } from "@server/actions.js",
  entities: [Job]
}

action stripePayment {
  fn: import { stripePayment } from "@server/actions.js",
  entities: [User]
}

action stripeCreditsPayment {
  fn: import { stripeCreditsPayment } from "@server/actions.js",
  entities: [User]
}

// üìö Queries

query getJobs {
  fn: import { getJobs } from "@server/queries.js",
  entities: [Job]
}

query getJob {
  fn: import { getJob } from "@server/queries.js",
  entities: [Job]
}

query getCoverLetter {
  fn: import { getCoverLetter } from "@server/queries.js",
  entities: [CoverLetter]
}

query getCoverLetters {
  fn: import { getCoverLetters } from "@server/queries.js",
  entities: [CoverLetter]
}

query getUserInfo {
  fn: import { getUserInfo } from "@server/queries.js",
  entities: [User]
}

query getCoverLetterCount {
  fn: import { getCoverLetterCount } from "@server/queries.js",
  entities: [CoverLetter]
}

/* üïµÔ∏è‚Äç‚ôÇÔ∏è These are the Wasp Cron Jobs. Use them to set up recurring tasks or queues:
 * https://wasp-lang.dev/docs/language/features#jobs
 */

job checkUserSubscription {
  executor: PgBoss,
  perform: {
    fn: import { updateUserSubscription } from "@server/workers/updateUserSubscription.js"
  },
  schedule: {
    cron: "0 23 * * *" 
  },
  entities: [User]
}

job checkExpirationDate {
  executor: PgBoss,
  perform: {
    fn: import { sendExpirationNotice } from "@server/workers/sendExpirationNotice.js"
  },
  schedule: {
    cron: "0 17 * * *" 
  },
  entities: [User]
}

job emailSender {
  executor: PgBoss,
  perform: {
    fn: import { sendGrid } from "@server/workers/sendGrid.js"
  },
  entities: [User]
}